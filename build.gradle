plugins {
	id 'org.springframework.boot' version '4.0.1'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'java'
	id 'war'
}

apply from: '../common/gradle/tomcat.config.gradle'

ext {
	file_name_build = 'odeon-wss-build.war'
	file_name_prod = 'odeon-wss.war'
	file_name_int = 'odeon-int-wss.war'
}


tasks.named('processResources') {
	filesMatching('project.properties') {
		expand(project.properties)
	}
}

bootWar {
	archiveFileName = file_name_build
}

group = 'com.romanpulov'
version = '0.15.0'

repositories {
	mavenCentral()
	mavenLocal()
}

dependencies {
	//spring boot
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-webmvc'

	//dialects to support SQLite
	implementation 'org.hibernate.orm:hibernate-community-dialects'

	//sqlite
	implementation 'org.xerial:sqlite-jdbc:3.50.1.0'

	//json
	implementation 'org.json:json:20231013'

	//custom configuration
	annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

	//api doc
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.14'

	//jutils
	implementation 'com.romanpulov:jutils-core:0.4.4'

	// https://mvnrepository.com/artifact/com.google.code.findbugs/jsr305
	implementation 'com.google.code.findbugs:jsr305:3.0.2'

	//test
	providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
	testImplementation("org.springframework.boot:spring-boot-starter-webmvc-test")
	testImplementation("org.springframework.boot:spring-boot-starter-test") {
		exclude group: "com.vaadin.external.google", module:"android-json"
	}
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.register('deployInt', Copy) {
	dependsOn(bootWar)
	from "build/libs"
	into tomcat_webapps
	include file_name_build
	rename file_name_build, file_name_int
}

tasks.register('deployProd', Copy) {
	dependsOn(bootWar)
	from "build/libs"
	into tomcat_webapps
	include file_name_build
	rename file_name_build, file_name_prod
}

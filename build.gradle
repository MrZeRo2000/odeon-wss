plugins {
	id 'org.springframework.boot' version '2.6.7'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'war'
}

ext {
	file_name_build = 'odeon-wss-build.war'
	file_name_prod = 'odeon-wss.war'
	file_name_int = 'odeon-int-wss.war'
}

ext {
	if (project.hasProperty("tomcat_home")) {
		tomcat_webapps = tomcat_home + '/webapps'
	} else {
		tomcat_webapps = 'D:/prj/apache-tomcat-9.0.24/webapps'
	}
}

tasks.named('processResources') {
	filesMatching('project.properties') {
		expand(project.properties)
	}
}


bootWar {
	archiveFileName = file_name_build
}

group = 'com.romanpulov'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
	mavenCentral()
	mavenLocal()
}

dependencies {
	//spring boot
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	//https://stackoverflow.com/questions/53326271/spring-nullable-annotation-generates-unknown-enum-constant-warning
	implementation 'com.google.code.findbugs:jsr305:3.0.2'

	//sqlite
	implementation 'org.xerial:sqlite-jdbc:3.36.0.3'

	//ms access
	implementation 'com.healthmarketscience.jackcess:jackcess:4.0.1'

	//json
	implementation 'org.json:json:20220320'

	//custom configuration
	annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

	//api doc
	implementation 'org.springdoc:springdoc-openapi-ui:1.6.9'

	//jutils
	implementation 'com.romanpulov:jutils-core:0.3.13'

	//test
	providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.register('deployInt', Copy) {
	dependsOn(bootWar)
	from "build/libs"
	into tomcat_webapps
	include file_name_build
	rename file_name_build, file_name_int
}
